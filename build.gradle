buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/repo" }
    mavenCentral()
    maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
  }
  dependencies {
    classpath 'io.ratpack:ratpack-gradle:0.9.0-SNAPSHOT'
    classpath group:'org.gradle.api.plugins', name:'gradle-heroku', version:'0.9.6'
    classpath group: 'com.github.timurstrekalov', name: 'gradle-saga-plugin', version: '1.5.0'
  }
}

repositories {
  maven { url "http://oss.jfrog.org/repo" }
  mavenCentral()
  maven { url "http://repo.springsource.org/repo" } // for springloaded
  maven { url "http://repo.jenkins-ci.org/releases/" }
}

apply plugin: 'heroku'

heroku {
    apiKey = ''// in gradle.properties
    appName = 'job-dsl'
}

// The “ratpack” plugin applies the “application” plugin, making it easy to create a standalone application.
// See: http://gradle.org/docs/current/userguide/application_plugin.html
apply plugin: "ratpack-groovy"

// The “ratpack” plugin is IDEA aware.
// It will create a run configuration in IDEA to launch your app in your IDE, with hot reloading.
apply plugin: "idea"

idea {
    project {
        jdkName "1.7"
        languageLevel "1.7"
    }
}

apply plugin: 'saga'

saga {
    baseDir = 'tests'
    outputDir = new File('build/coverage')
    includes = '**/*Test.html'
}

configurations.all {
    exclude module: "groovy"
}

dependencies {
    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    springloaded group:'org.springsource.springloaded', name:'springloaded-core', version:'1.1.4'
    testCompile(group:'org.spockframework', name:'spock-core', version:'0.7-groovy-2.0') {
        exclude module: "groovy-all"
    }
    compile 'org.jenkins-ci.plugins:job-dsl-core:1.20'
}

defaultTasks "clean", "build", "installApp"